apply plugin: 'maven-publish'
apply plugin: "com.jfrog.bintray"

def getReleaseRepositoryUrl() {
    return hasProperty('RELEASE_REPOSITORY_URL') ? RELEASE_REPOSITORY_URL
            : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

def getSnapshotRepositoryUrl() {
    return hasProperty('SNAPSHOT_REPOSITORY_URL') ? SNAPSHOT_REPOSITORY_URL
            : "https://oss.sonatype.org/content/repositories/snapshots/"
}

def getRepositoryUsername() {
    return hasProperty('SONATYPE_NEXUS_USERNAME') ? SONATYPE_NEXUS_USERNAME : ""
}

def getRepositoryPassword() {
    return hasProperty('SONATYPE_NEXUS_PASSWORD') ? SONATYPE_NEXUS_PASSWORD : ""
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

    failOnError false
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourceJar(type: Jar) {
    classifier 'sources'
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate { project ->
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = GROUP_ID
                artifactId = ARTIFACT_ID
                version = VERSION

                artifact androidJavadocsJar
                artifact androidSourceJar

                pom {
                    name = POM_NAME
                    packaging = POM_PACKAGING
                    url = POM_URL
                }
            }
        }
    }

    bintray {
        user = getRepositoryUsername()
        key = getRepositoryPassword()
        publications = ['release']

        pkg {
            repo = 'maven'
            name = 'bluetooth-spp'
            userOrg = 'caoshen'
            licenses = ['Apache-2.0']
            websiteUrl = 'https://github.com/caoshen/bluetooth/'
            issueTrackerUrl = 'https://github.com/caoshen/bluetooth/issues'
            vcsUrl = 'https://github.com/caoshen/bluetooth'
            version {
                name = VERSION
                desc = 'Bluetooth spp library'
            }
        }
    }
}




